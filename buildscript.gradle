project.buildscript {
    repositories {
        maven { url "https://jitpack.io" }
        google()
        mavenCentral()
    }
    ext.GadgetScript = GadgetScript
    ext.GadgetVer = [
        "minSdk" : 24,
        "compileSdk" : 32,
        "buildGradle" : "7.1.1",
        "kotlin" : "1.7.10",
        "ksp" : "1.7.10-1.0.6",
        "kotlinCompilerExtension" : "1.3.1",

        "android" : [
            "appCompat" : "1.5.1",
            "material" : "1.8.0",
            "constraintLayout" : "2.0.1",
            "swipeRefreshLayout" : "1.1.0",
            "viewPager2" : "1.0.0",
            "lifecycle" : "2.5.1",
            "dataStore" : "1.0.0",
        ],

        "google" : [
            "autoService" : "1.0",
            "gson" : "2.9.0",
        ],

        "squareUp" : [
            "okHttp" : "4.10.0",
            "kotlinPoet" : "1.12.0",
            "javaPoet" : "1.13.0",
            "retrofit2" : "2.9.0",
        ],

        "bumptech" : [
            "glide" : "4.13.0"
        ],
    ]
}

project.subprojects {
    repositories {
        maven { url "https://jitpack.io" }
        google()
        mavenCentral()
    }
}

final class GadgetScript {

    static void dependsGadgetGradle(Project project) {
        project.dependencies {
            compileOnly "com.github.Zhupff:Gadget-Gradle:${project.rootProject.ext.GADGET_GRADLE_VERSION}"
        }
    }

    static void compose(String version, @DelegatesTo(GadgetScriptDelegate.class) Closure closure) {
        closure.delegate = new GadgetScriptDelegate(version, closure.owner)
        closure()
    }

    private final static class GadgetScriptDelegate {

        @Delegate Project project

        GadgetScriptDelegate(version, script) {
            this.project = script.project
            println("GadgetScript compose in ${this.project.name}")
            this.project.buildscript {
                rootProject.ext.GADGET_GRADLE_VERSION = version
                dependencies {
                    classpath("com.github.Zhupff:Gadget-Gradle:${rootProject.ext.GADGET_GRADLE_VERSION}")
                }
            }
        }

        def GadgetWidget(version) {
            project.buildscript {
                rootProject.ext.GADGET_WIDGET_VERSION = version
                dependencies {
                    classpath("com.github.Zhupff.Gadget-Widget:gradle:${rootProject.ext.GADGET_WIDGET_VERSION}")
                }
            }
        }

        def GadgetTheme(version) {
            project.buildscript {
                rootProject.ext.GADGET_THEME_VERSION = version
                dependencies {
                    classpath("com.github.Zhupff.Gadget-Theme:gradle:${rootProject.ext.GADGET_THEME_VERSION}")
                }
            }
        }
    }
}
